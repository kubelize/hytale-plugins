plugins {
  id 'java'
}

group = 'com.kubelize'
version = '0.1.0'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly files('libs/HytaleServer.jar')

  // Bundled runtime deps for DB access.
  implementation 'com.zaxxer:HikariCP:5.1.0'
  implementation 'org.postgresql:postgresql:42.7.3'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.withType(AbstractArchiveTask).configureEach {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
}

tasks.named('processResources') {
  exclude 'Assets/**'
  exclude 'Common/**'
  doFirst {
    delete file("$buildDir/resources/main/Assets")
    delete file("$buildDir/resources/main/Common")
  }
}

tasks.jar {
  archiveClassifier.set('')
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  exclude 'Assets/**'
  exclude 'Common/**'
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs += ["-Xlint:unchecked"]
}
